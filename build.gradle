buildscript {
	repositories {
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies { classpath 'org.minimallycorrect.gradle:DefaultsPlugin:0.1.5' }
	configurations.classpath {
		resolutionStrategy.activateDependencyLocking()
	}
}
plugins {
	id 'java'
	id 'java-library'
	id 'groovy'
	id 'maven-publish'
	id "com.gradle.plugin-publish" version "0.12.0"
	id 'java-gradle-plugin'
}

sourceSets.main.java.srcDirs = files('src')
sourceSets.test.java.srcDirs = files('test')

apply plugin: 'org.minimallycorrect.gradle.DefaultsPlugin'

group = 'dev.minco.mapping'
version = '0.0.1-SNAPSHOT'

minimallyCorrectDefaults {
	languageLevel = JavaVersion.VERSION_11
	// TODO: make relevant?
	description = ""
	labels = []
	jacoco = false
	configureProject(project)
}

repositories {
	maven {
		url 'https://maven.fabricmc.net/'
		name 'fabricmc'
		content {
			includeGroup("net.fabricmc")
		}
	}
}

configurations.compile.dependencies.remove dependencies.gradleApi()
configurations.all { it.dependencies.remove project.dependencies.gradleApi() }
dependencies { compileOnly gradleApi() }

dependencies {
	compileOnly "org.jetbrains:annotations:20.1.0"
	compileOnly "org.checkerframework:checker-qual:3.21.2"
	testImplementation "junit:junit:4.13.1"
	// TODO: actually test Mixin at the same time
	//api 'org.minimallycorrect.mixin:Mixin:0.0.6'
	implementation 'net.fabricmc:tiny-remapper:0.3.2'
	implementation 'net.fabricmc:tiny-mappings-parser:0.2.2.14'
	implementation 'com.google.guava:guava:30.1-jre'

	compileOnly 'org.projectlombok:lombok:1.18.16'
	annotationProcessor 'org.projectlombok:lombok:1.18.16'
	testCompileOnly 'org.projectlombok:lombok:1.18.16'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'

	// https://junit.org/junit5/docs/current/user-guide/#running-tests-build
	testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.0")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.0")
}

test {
	useJUnitPlatform()
}

allprojects {
	tasks.withType(Test).all {
		maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
	}

	configurations {
		all {
			resolutionStrategy {
				failOnVersionConflict()

				force('com.google.guava:guava:30.0-jre')
				force('com.google.code.findbugs:jsr305:3.0.2')
				def asmVer = '9.0'
				force("org.ow2.asm:asm:$asmVer")
				force("org.ow2.asm:asm-tree:$asmVer")
				force("org.ow2.asm:asm-analysis:$asmVer")
				force("org.ow2.asm:asm-util:$asmVer")
			}
		}
	}
}

gradlePlugin {
	plugins {
		mapping {
			id = 'dev.minco.mapping'
			implementationClass = 'dev.minco.mapping.Plugin'
		}
	}
}

pluginBundle {
	website = 'https://minco.dev/'
	vcsUrl = 'https://github.com/MinimallyCorrect/Mapping'
	// TODO: defaults plugin should copy those over
	description = 'TODO'
	tags = ['TODO']

	plugins {
		mapping {
			displayName = 'Mapping'
		}
	}
}

publishing {
	publications {
		pluginMaven(MavenPublication) {
			// sources weren't attached by default? /shrug
			artifact sourcesJar
		}
	}
}
